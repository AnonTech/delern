def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"

android {
    compileSdkVersion 28

    lintOptions {
        disable 'InvalidPackage'
    }

    signingConfigs {
        release {
            // Even when the value of file() is unused (debug build, dependency
            // resolution etc), the function call is still evaluated. When this
            // environment variable is unset (null), file() complains, so we
            // have to provide a default value.
            storeFile file(System.getenv('ANDROID_KEYSTORE_PATH') ?: 'release.keystore')
            keyAlias 'upload'
            storePassword 'upload'
            keyPassword 'upload'
        }
    }

    defaultConfig {
        applicationId "org.dasfoo.delern"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        // This is a temporary filter. libflutter.so is only available for one
        // architecture at a time, but Crashlytics supplies libraries for all.
        // Android on arm64 optimistically loads Crashlytics for 64bit, but does
        // not find libflutter.so for 64bit and crashes.
        // The filter below removes Crashlytics (or any other) libraries for
        // platforms other than armeabi-v7a, which is a universally supported
        // (but perhaps slower) platform (by all devices).
        // TODO(dotdoom): remove when Flutter supports bundles.
        // https://github.com/flutter/flutter/issues/18494.
        ndk {
            abiFilters 'armeabi-v7a'
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            manifestPlaceholders = [appName: "Delern"]
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            manifestPlaceholders = [appName: "Delern DEBUG"]
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.2-alpha01'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.2-alpha01'
}

apply plugin: 'com.google.gms.google-services'
